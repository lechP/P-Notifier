apply plugin: 'idea'
apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'clean', 'build', 'copyDeps', 'copyBin'

task copyBin(type: Copy) {
    into "$buildDir/libs"
    from "src/main/bin/"
}

task copyDeps(type: Copy) {
    into "$buildDir/libs/lib"
    from configurations.runtime
}

jar.doFirst {
    def manifestClasspath = configurations.runtime.collect { "lib/" + it.getName() }.join(' ')
    manifest {
        attributes(
                'Main-Class': 'com.picadilla.notifier.main.Main',
                'Class-Path': manifestClasspath
        )
    }
}

repositories {
    mavenCentral()
}

ext {
    hibernateVersion = '5.1.0.Final'
    springVersion = '4.2.4.RELEASE'
    jasyptVersion = '1.9.2'
}

dependencies {
    //persistence
    compile 'org.hibernate:hibernate-core:' + hibernateVersion
    compile 'org.hibernate:hibernate-entitymanager:' + hibernateVersion
    compile 'mysql:mysql-connector-java:5.1.38'
    compile 'org.springframework:spring-orm:' + springVersion
    compile 'org.springframework:spring-tx:' + springVersion
    //DI
    compile 'org.springframework:spring-core:' + springVersion
    compile 'org.springframework:spring-beans:' + springVersion
    compile 'org.springframework:spring-context:' + springVersion
    //email
    compile 'org.springframework:spring-context-support:' + springVersion
    compile 'com.sun.mail:javax.mail:1.5.3'
    //encryption
    compile 'org.jasypt:jasypt:' + jasyptVersion
    compile 'org.jasypt:jasypt-spring31:' + jasyptVersion
    //various
    compile 'log4j:log4j:1.2.17'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'commons-validator:commons-validator:1.4.1'
    //test
    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:2.0.0'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.springframework:spring-test:' + springVersion
    testCompile 'com.icegreen:greenmail:1.4.1'
}
